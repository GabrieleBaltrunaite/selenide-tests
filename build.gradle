plugins {
    id 'java'
    id 'io.qameta.allure' version '2.12.0'
}

group 'com.imdb'
version '1.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

configurations {
    agent
}

dependencies {
    testImplementation 'com.codeborne:selenide:7.7.2'
    testImplementation 'org.testng:testng:7.11.0'

    // Allure Dependencies
    testImplementation 'io.qameta.allure:allure-selenide:2.29.1'
    testImplementation 'io.qameta.allure:allure-testng:2.29.1'

    // Required for Allure @Step annotations in Java 17
    testRuntimeOnly 'org.aspectj:aspectjweaver:1.9.22.1'
    agent 'org.aspectj:aspectjweaver:1.9.22.1'

    testImplementation 'org.slf4j:slf4j-simple:2.0.16'
}

test {
    useTestNG()
    systemProperties(System.getProperties())
    systemProperty "org.slf4j.simpleLogger.defaultLogLevel", "warn"
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
        showStackTraces true
    }
    jvmArgs = [
            "-javaagent:${configurations.agent.singleFile.absolutePath}"
    ]
    ignoreFailures = true
}

allure {
    version = '2.32.0'
    adapter {
        frameworks {
            testng {
                enabled = true
            }
        }
    }
}
// For CI/CD (Docker/Jenkins):
// test.finalizedBy allureReport

// For Local Development (Auto-open browser):
// test.finalizedBy allureServe

